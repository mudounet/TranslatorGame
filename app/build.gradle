apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE']=code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

	    defaultConfig {
    	    applicationId "com.mudounet.translatorgame"
       		minSdkVersion 24
        	targetSdkVersion 26
        	versionCode code
        	versionName "1.1.2"
        	testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    	}
    }
    else {
        throw new FileNotFoundException("Could not read version.properties!")
    }
    
    signingConfigs {
        release {
            // See README.md#Signing Application for instructions on how to sign your release as below
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation('org.simpleframework:simple-xml:2.7') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    implementation 'com.android.support:appcompat-v7:26.1.0'

    runtimeOnly 'com.github.tony19:logback-android-core:1.1.1-6'
    runtimeOnly ('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }
    implementation 'org.slf4j:slf4j-api:1.7.21'
    implementation 'com.google.android:flexbox:0.3.2'

    testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'


}
